%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%
%        GMAT script to
%
%
%
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

% main code
Create Spacecraft LEOsat
LEOsat.DisplayStateType = Cartesian
LEOsat.X = 1137.8250457656243
LEOsat.Y = 4665.924676676307
LEOsat.Z = 4793.767002884218
LEOsat.VX = -7.149857639848608
LEOsat.VY = -0.9383420850231425
LEOsat.VZ = 2.602453133083086

Create ForceModel LEOprop_ForceModel
LEOprop_ForceModel.CentralBody = Earth
LEOprop_ForceModel.PrimaryBodies = {Earth}
LEOprop_ForceModel.PointMasses = {Luna, Sun}
LEOprop_ForceModel.SRP = On
LEOprop_ForceModel.GravityField.Earth.Degree = 4
LEOprop_ForceModel.GravityField.Earth.Order = 4
LEOprop_ForceModel.GravityField.Earth.PotentialFile = 'JGM2.cof'
LEOprop_ForceModel.Drag.AtmosphereModel = JacchiaRoberts
LEOprop_ForceModel.Drag.F107 = 150
LEOprop_ForceModel.Drag.F107A = 150

Create Propagator LEOprop
GMAT LEOprop.FM = LEOprop_ForceModel

Create ImpulsiveBurn TCM1
Create ImpulsiveBurn TCM2
Create ImpulsiveBurn FinalBurn

Create DifferentialCorrector DC

Create OrbitView DefaultOrbitView
DefaultOrbitView.Add = {LEOsat, Earth}

Create XYPlot XYPlot1
GMAT XYPlot1.XVariable = LEOsat.A1ModJulian
GMAT XYPlot1.YVariables = {LEOsat.Earth.Altitude}

Create GroundTrackPlot GroundTrackPlot1
GroundTrackPlot1.Add = {LEOsat}

Create Variable deltav
Create Variable newdeltav

Create ReportFile rf

Create ReportFile rf2
rf2.Add = {LEOsat.UTCModJulian, LEOsat.Earth.Altitude, ...
LEOsat.Earth.RMAG, LEOsat.Earth.ECC, TCM1.Element1, TCM2.Element1, deltav, LEOsat.INC, newdeltav}

Create EphemerisFile anEphemerisFile
GMAT anEphemerisFile.Spacecraft = LEOsat

Create GmatFunction TargetLEOStationKeeping
TargetLEOStationKeeping.FunctionPath = ...
'C:\\Users\\eelstein\\Documents\\Bama\\AEM582\\AEM582_Project\\TargetLEOStationKeeping.gmf' 

Create Variable desiredRMAG desiredECC X Y Z
Create Variable totaldv

BeginMissionSequence

desiredRMAG = 6785
desiredECC = 0.00005
totaldv = 0.008

% Declare LEOsat, Subscribers and other objects as Global:
Global LEOsat
Global DC TCM1 TCM2 LEOprop_ForceModel
Global DefaultOrbitView XYPlot1 GroundTrackPlot1
Global rf rf2 anEphemerisFile
Global deltav

deltav = 0

While deltav < totaldv

Propagate 'Prop One Step' LEOprop(LEOsat)
	
If 'If Alt < Threshold' LEOsat.Earth.Altitude < 400

Propagate 'Prop To Periapsis' LEOprop(LEOsat) {LEOsat.Periapsis}

% Call function to implement SK. Pass local variables as input:
TargetLEOStationKeeping(desiredRMAG,desiredECC)

deltav = deltav + abs(TCM1.Element1) + abs(TCM2.Element1)

EndIf
	
EndWhile

newdeltav = totaldv + .01 - deltav

Propagate 'Prop To Periapsis' LEOprop(LEOsat) {LEOsat.Periapsis}

FinalBurn.Element1 = newdeltav
FinalBurn.Element2 = 0
FinalBurn.Element3 = 0

Maneuver 'Apply FinalBurn' FinalBurn(LEOsat)

Propagate LEOprop(LEOsat) {LEOsat.Earth.Altitude = 250}

Report rf LEOsat.UTCGregorian LEOsat.UTCModJulian LEOsat.X ...
LEOsat.Y LEOsat.Z LEOsat.Earth.Altitude LEOsat.Earth.ECC
